######################
# Andor's Trail resource grammar
######################

$include "basic.abnf";

###
# Here is an error! Andor's Trail repo contains prototype declaration but published version dosn't.
# Maybe it will be fixed in the future versions.
###
resource = *(CRLF / SP) [resDeclaration] *(CRLF / SP) *(resDefinition *(CRLF / SP));

resDeclaration = prototype *SP SEMICOLON;
prototype = BRACKET_L *SP 1*(*SP (protoVal / protoArr) *SP PIPE) *SP BRACKET_R;
protoVal = qualifier;
protoArr = qualifier *SP prototype;
qualifier = (ALPHA / UNDERSCORE) *(ALPHA / DIGIT / UNDERSCORE);

resDefinition = definition *SP SEMICOLON;
definition = CRL_BRACKET_L *SP *(*SP ((arrVal *SP PIPE) / (intVal *SP PIPE) / (strVal *SP PIPE) / (emptyVal *SP PIPE)) ) *SP CRL_BRACKET_R;
strVal = 1*(ALPHA / DIGIT / UNDERSCORE / "-" / SP / DOT / COMMA / COLON / SEMICOLON / "(" / ")" / $rule("ExtRule_U_L"));
intVal = [%x2d] 1*(DIGIT);
arrVal = definition;
emptyVal = "";
